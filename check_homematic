#!/usr/bin/perl -w

# Written by Darko Krizic <darko@krizic.net>

use strict;
use Getopt::Long;
use RPC::XML;
use RPC::XML::Client;
use Data::Dumper;

sub usage() {
	print <<EOF;
Usage: check_homematic [-v] --host=<hostname> --port=<port> --mode=<lowbat|unreach> --level=<critical|warning> 

	host: The Homematic XML RPC host
	port: The port number of the XML RPC host
	mode: Check for lowbat or for unreach
	level: If a lowbat or unreach occurs, is it a warning or critical
EOF
	exit 255;
}

my $verbose = 0;
my $host = undef;
my $port = undef;
my $mode = undef;
my $level = undef;

GetOptions(
	'verbose|v!' => \$verbose,
	'host|h=s' => \$host,
	'port|p=i' => \$port,
	'mode|m=s' => \$mode,
	'level|l=s' => \$level,
);

usage() unless defined $host;
usage() unless defined $port;
usage() unless defined $mode;
usage() unless defined $level;
usage() unless $level eq 'warning' || $level eq 'critical';
usage() unless $mode eq 'lowbat' || $mode eq 'unreach';

my $url = "http://$host:$port";
my $cli = RPC::XML::Client->new($url);

my %address;
{
	print "Connecting to $url\n" if $verbose;
	my $resp = $cli->send_request('listDevices');
	foreach my $device (@{$resp}) {
		#print Dumper( $device );
		my $address = $device->{'ADDRESS'}->value;
		#my $name = $device->{'NAME'}->value;
		my $channel = undef;
		if( $address =~ m/:/ ) {
			($address,$channel) = split(":",$address);
			$address{$address}->{'channel'} = [] unless defined $address{$address}->{'channel'};;
			push @{$address{$address}->{'channel'}},$channel;
		} else {
			$address{$address}->{'id'} = $device->{'ID'}->value;
		}
	}
}
printf "Reading details for %i devices\n",scalar keys %address if $verbose;
foreach my $address (keys %address) {
	my $id = $address{$address}->{'id'};
	my @msg;
	push @msg, RPC::XML::int->new( $id );
	push @msg, RPC::XML::int->new( 0 );
	push @msg, RPC::XML::string->new( 'VALUES' );
	my $resp = $cli->send_request('getParamset',@msg);
	print Dumper( $resp ) if $verbose;
	$address{$address}->{'unreach'} = $resp->value->{'UNREACH'};
	$address{$address}->{'lowbat'} = $resp->value->{'LOWBAT'};
}

my @lowbat = ();
my $batcount = 0;
my @unreach = ();

print Dumper("devices", \%address) if $verbose;

foreach my $address (keys %address) {
	if( defined $address{$address}->{'lowbat'} ) {
		$batcount++;
		if( $address{$address}->{'lowbat'} ne 0 ) {
			push @lowbat,$address;
		}
	}
	if( defined $address{$address}->{'unreach'} && $address{$address}->{'unreach'} ne 0 ) {
		push @unreach,$address;
	}
}

my $lowbat = scalar @lowbat;
my $unreach = scalar @unreach;

printf "%i devices have low battery\n", $lowbat if $verbose;
printf "%i devices are unreachable\n", $unreach if $verbose;

my $ret = 1;	
my $retname = 'WARNING';
if( $level eq 'critical' ) {
	$ret = 2;
	$retname = 'WARNING';
}

if( $mode eq 'lowbat' ) {
	my $perf = "'Low battery'=$lowbat";
	if( $lowbat eq 0 ) {
		printf "OK: All %i battery powered devices with sufficient batteries | $perf\n", $batcount;
		exit 0;
	}
	printf "$retname: %i of %i devices (%s) have battery problems | $perf\n", $lowbat, $batcount, join(", ", @lowbat);
	exit $ret;
} elsif( $mode eq "unreach" ) {
	my $perf = "'Unreachable'=$unreach";
	if( $unreach eq 0 ) {
		printf "OK: All %i devices reachable | $perf\n", scalar keys %address;
		exit 0;
	}
	printf "$retname: %i of %i devices (%s) unreachable | $perf\n", $unreach, scalar keys %address, join(", ", @unreach);
	exit $ret;
} 

print "ERROR: Unknown mode $mode\n";
exit 2;
